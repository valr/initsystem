# Maintainer: valr <valere dot monseur at ymail dot com>

pkgname=opentmpfiles
pkgver=0.3.1
pkgrel=1
pkgdesc='a standalone utility for handling systemd-style tmpfiles.d settings'
url='https://github.com/OpenRC/opentmpfiles'
license=('BSD')
groups=('initsystem')
arch=('any')
depends=('bash')
source=(https://github.com/OpenRC/"${pkgname}"/archive/"${pkgver}".tar.gz
        tmpfiles.hook)
md5sums=('6625db2353c4eec535992ee0d60f99ba'
         '8458c5781a5df8d7fce61fecb50cc639')

package() {
  cd "${pkgname}-${pkgver}"

  make bindir=/usr/bin DESTDIR="${pkgdir}" install

  install -d -m 755 "${pkgdir}"/etc/tmpfiles.d
  install -d -m 755 "${pkgdir}"/usr/lib/tmpfiles.d

  install -D -m 644 "${srcdir}"/tmpfiles.hook "${pkgdir}"/usr/share/libalpm/hooks/tmpfiles.hook
  install -D -m 644 "${srcdir}/${pkgname}-${pkgver}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
