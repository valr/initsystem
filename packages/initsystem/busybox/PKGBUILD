# Maintainer: Val√®re Monseur <archlinux at vale dot re>

pkgname=busybox
pkgver=1.33.1
pkgrel=1
pkgdesc='utilities for rescue and embedded systems'
url='http://www.busybox.net/'
license=('GPL')
arch=('x86_64' 'aarch64')
groups=('initsystem')
source=("${url}/downloads/${pkgname}-${pkgver}.tar.bz2"
        'config_nosuid'
        'config_suid'
        'Makefile.flags.patch')
        # http://busybox.net/downloads/fixes-"${pkgver}"/busybox-"${pkgver}"-{CVE-2016-2147,CVE-2016-2148,ash-recursive-heredocs}.patch
md5sums=('7f9f8acf026ed11dfa9b672c41b204e0'
         '3e41627817ba17f226a35018b5d347c1'
         '77fc4557e653e99ece90806095eb29ec'
         '711fd2475933fad30cfe2229c346e5b6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for PATCH in "${srcdir}"/*.patch; do
    patch -p1 <${PATCH}
  done

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/nosuid
  cp "${srcdir}"/config_nosuid "${srcdir}"/nosuid/.config

  cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}"/suid
  cp "${srcdir}"/config_suid "${srcdir}"/suid/.config
}

build() {
  cd "${srcdir}"/nosuid
  make

  cd "${srcdir}"/suid
  make
}

package() {
  # setuid applets:
  # applets that have 'BB_SUID_REQUIRE' in include/applets.src.h
  # crontab, login, passwd, su, vlock, wall

  # binaries

  cd "${srcdir}"/nosuid
  install -D -m 755 busybox "${pkgdir}"/usr/bin/busybox

  cd "${srcdir}"/suid
  install -D -m 4755 busybox "${pkgdir}"/usr/bin/busybox-suid

  # symlinks: install applets in /usr/bin

  cd "${pkgdir}"/usr/bin
  ln -s /usr/bin/busybox ash

  # symlinks: install applets in /opt/busybox

  install -d -m 755 "${pkgdir}"/opt/busybox
  cd "${pkgdir}"/opt/busybox

  for APPLET in $("${pkgdir}"/usr/bin/busybox --list); do
    case "${APPLET}" in
      halt|poweroff|reboot)
        ;;
      *)
        ln -s /usr/bin/busybox "${APPLET}"
        ;;
    esac
  done

  for APPLET in $("${pkgdir}"/usr/bin/busybox-suid --list); do
    ln -s /usr/bin/busybox-suid "${APPLET}"
  done

  install -D -m 644 "${srcdir}"/nosuid/README "${pkgdir}"/usr/share/doc/"${pkgname}"/README
  install -D -m 644 "${srcdir}"/nosuid/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
