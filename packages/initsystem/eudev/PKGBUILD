# Maintainer: Val√®re Monseur <archlinux at vale dot re>

# dependency: UDEV_VERSION in eudev/configure.ac has to be >= libudev_req in libgudev/meson.build
_pkgver=255.2 # systemd pkgver

pkgname=eudev
pkgver=3.2.14
pkgrel=1
pkgdesc='standalone dynamic and persistent device naming support (aka userspace devfs) daemon'
url='https://github.com/eudev-project/eudev'
license=('GPL2')
groups=('initsystem')
arch=('x86_64')
backup=('etc/udev/udev.conf')
depends=('glib2' 'kmod' 'libxslt' 'util-linux')
makedepends=('gperf' 'linux-headers')
provides=("udev=${_pkgver}" 'libudev.so')
conflicts=('udev' 'libudev.so')
replaces=('udev' 'libudev.so')
source=("https://github.com/eudev-project/eudev/releases/download/v${pkgver}/eudev-${pkgver}.tar.gz"
        "https://github.com/eudev-project/eudev/releases/download/v${pkgver}/eudev-${pkgver}.tar.gz.asc"
        'udev-hwdb.hook'
        'udev-initcpio.hook'
        'udev-initcpio.install')
md5sums=('ee1789b524304dfe6d4775b4b39f6d3c'
         'SKIP'
         'df0f7cc9b3cc57bc11ac371670d24663'
         '7a238690f224080368913abe55dc94e9'
         '9a8bb20741d3371d37de8b6a66c214a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-split-usr \
    --enable-kmod \
    --enable-hwdb \
    --enable-manpages \
    --with-rootprefix=/usr

    make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for RULES in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${RULES}"
  done

  install -D -m 644 "${srcdir}"/udev-hwdb.hook "${pkgdir}"/usr/share/libalpm/hooks/udev-hwdb.hook
  install -D -m 644 "${srcdir}"/udev-initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/udev
  install -D -m 644 "${srcdir}"/udev-initcpio.install "${pkgdir}"/usr/lib/initcpio/install/udev
  install -D -m 644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim: set filetype=sh:
